CreateFunction({
  name: 'get_all_dinos',
  body: Query(
    Lambda(
      [],
      Let(
        {
          match: If(
            HasIdentity(),
            If(
              Equals('admin', Select(['data', 'type'], Get(Identity()), null)),
              // Admins can see everything
              Documents(Collection('dinos')),
              // Normal users only see non legendary
              Difference(Documents(Collection('dinos')), Match(Index('dinos_by_rarity'), 'legendary'))
            ),
            Match(Index('dinos_by_rarity'), 'common')
          )
        },
        q.Map(
          // map over the references returned from paginate
          Paginate(Var('match')),
          // for each of these references, get the document
          Lambda(['dinoRef'], Get(Var('dinoRef')))
        )
      )
    )
  ),
  role: 'server'
})
